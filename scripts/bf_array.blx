// Fucking Bollox

var tape = [0; 30000];
var instructions = [];

while true {
    var ch = getc();

    if ch == -1 {
        break;
    } else switch chr(ch) {
        "[", "]", ",", ".", "+", "-", "<", ">" {
            instructions.push(chr(ch));
        }
    }
}

var tape_pointer = 0;
var instruction_pointer = 0;
var buffered_output = "";

while instructions[instruction_pointer] {
    var inst = instructions[instruction_pointer];
    switch inst {
        "+" {
            tape[tape_pointer] = (tape[tape_pointer] + 1) % 256;
        }
        "-" {
            tape[tape_pointer] = (tape[tape_pointer] - 1) % 256;
        }
        ">" {
            tape_pointer += 1;
        }
        "<" {
            tape_pointer -= 1;
        }
        "," {
            tape[tape_pointer] = getc();
        }
        "." {
            var c = tape[tape_pointer];
            if c == 10 {
                print buffered_output;
                buffered_output = "";
            } else if c >= 0 and c <= 127 {
                buffered_output += chr(c);
            }
        }
        "[" {
            if tape[tape_pointer] == 0 {
                var depth = 0;
                while instructions[instruction_pointer + 1] {
                    instruction_pointer += 1;
                    if instructions[instruction_pointer] == "[" {
                        depth += 1;
                    }
                    if instructions[instruction_pointer] == "]" {
                        if depth == 0 {
                            break;
                        }
                        depth -= 1;
                    }
                }
            }
        }
        "]" {
            if tape[tape_pointer] != 0 {
                var depth = 0;
                while instructions[instruction_pointer - 1] {
                    instruction_pointer -= 1;
                    if instructions[instruction_pointer] == "]" {
                        depth += 1;
                    }
                    if instructions[instruction_pointer] == "[" {
                        if depth == 0 {
                            break;
                        }
                        depth -= 1;
                    }
                }
            }
        }
    }
    instruction_pointer += 1;
}

if buffered_output != "" { print buffered_output; }

